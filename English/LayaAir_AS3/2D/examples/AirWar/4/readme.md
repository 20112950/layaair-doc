# 4. The realization of game UI function



### Notes for editing page logic

​	In this case, we directly instantiate and displayed the four page classes generated by the IDE editor , and the implementation of the game interface process.

​        In this lesson, we will gradually realize the four page of the UI function, to start page GameStartUI.as for example, open the path for the class, "\src\ui\GameStartUI.as", you can see... For example code, the component variables defined page will generate public global variables of the class, so start the game button in gameStart (btn_start) method we also can directly transfer to the event listener. The text box txt_load can also be invoked to update the load schedule.

​       Then some students will ask, is it possible to add a logical code in this class? The author's answer is: do not!! Because when we republish the page in IDE, it will automatically cover the original generated class, and all the logical code you write will disappear...

 	So when we develop logical functions, we need to build a new derived class to edit the code in the derived class. The author also beginners too large when,  so students must take warnings !:)

```
/**Created by the LayaAirIDE,do not modify.*/
package ui {
	import laya.ui.*;
	import laya.display.*; 
	import laya.display.Text;

	//IDE自动创建生成的页面显示类（开始页面）
	public class GameStartUI extends Dialog 
	{
	    //IDE编辑器中设置的进度显示变量定义
		public var txt_load:Text;
		//IDE编辑器中设置的开始游戏按钮变量定义
		public var btn_start:Box;

		//IDE生成的页面布局JSON数据
		public static var uiView:Object ={"type":"Dialog","props":{"width":720,"height":1280},"child":[{"type":"Image","props":{"y":0,"x":0,"width":720,"skin":"gameUI/bg.jpg","sizeGrid":"4,4,4,4","height":1280}},{"type":"Image","props":{"y":378,"x":179,"skin":"gameUI/logo.png"}},{"type":"Text","props":{"y":587,"x":20,"width":681,"var":"txt_load","text":"游戏资源加载进度","height":29,"fontSize":"30","font":"SimHei","color":"#1c1c1c","align":"center"}},{"type":"Text","props":{"y":1200,"x":20,"width":681,"text":"LayaAir1.7.3引擎教学演示版","height":29,"fontSize":"26","font":"SimHei","color":"#7c7979","bold":true,"align":"center"}},{"type":"Box","props":{"y":960,"x":240,"visible":true,"var":"btn_start"},"child":[{"type":"Button","props":{"y":0,"x":0,"width":240,"visible":true,"stateNum":"2","skin":"gameUI/btn_bg.png","sizeGrid":"20,20,20,20","height":80}},{"type":"Image","props":{"y":19,"x":41,"skin":"gameUI/start.png"}}]}]};
		
		//根据JSON数据创建页面子对象
		override protected function createChildren():void
        {
			View.regComponent("Text",Text);
			super.createChildren();
			createView(uiView);
		}
	}
}
```

​	

### Start page “logical function”

In the src directory, a new start page class GameStart.as is built to inherit the GameStartUI.as generated by the IDE.

We mainly implement the logic in the start page ：

1.Game resources are loaded. The method of loading completion and loading progress callback is used in Loader.load (). The progress text box in UI is updated in the load progress method, and the "start game" button is displayed in the completion method.
Note: if the game starts with smaller resources and faster progress to 100%, it can also make a false progress of at least 1.5 seconds, which is consistent with the observation time of human vision.

2. Hide and display the start button function. Hidden before the resources are not completed, so as not to be loaded and click into the game.

3. Start the page animation effect. The slow motion animation appears when the button is displayed; the page pops up the animation effect, and the Dialog page has a slow motion animation that opens and closes.

In the main class, we modify the class name GameStartUI to GameStart.

Because the Dialog page has the ability to open and close the animation, the Laya.stage.addChild (start) is modified to be start.popup ().

Add the start.close () animation closure method in the game initialization function.


```
    private function gameStart():void
    {
        //实例化开始页面
        start=new GameStart();
        //以弹出方式打开，有缓动效果。IDE中页面为Dialog类型才可用
        start.popup();
        //监听开始游戏开始按钮事件,点击后进入游戏中
        start.btn_start.on(Event.MOUSE_UP,this,gameInit)
    }

    /**
    游戏中，游戏初始化
    */
    private function gameInit():void
    {
        //缓动动画关闭效果。IDE中页面为Dialog类型才可用
        start.close();
        ...
```
To start the page Remove stage and destroy, it needs to be called at the end of the closing animation, so we need to add a closed event listener and callback method in GameStart.as.

Start the entire code interface:

Note: in the code, assetArr is an array of game resources, including the game atlas and music. So the music files need to be copied to the “bin/h5” directory, otherwise the load will be reported wrong.

```
package
{
	import laya.events.Event;
	import laya.net.Loader;
	import laya.utils.Ease;
	import laya.utils.Handler;
	import laya.utils.Tween;
	
	import ui.GameStartUI;
	
	/**
	 * 游戏开始界面
	 */
	public class GameStart extends GameStartUI
	{
		/***游戏资源地址数组***/
		private var assetArr:Array=[{url:"res/atlas/gameRole.atlas"},
									{url:"sound/achievement.mp3", type:Loader.SOUND}, 
									{url:"sound/bullet.mp3", type:Loader.SOUND},
									{url:"sound/game_over.mp3", type:Loader.SOUND},
									{url:"sound/enemy1_die.mp3", type:Loader.SOUND},
									{url:"sound/enemy3_out.mp3", type:Loader.SOUND}
								   ]
		
		/***游戏开始界面***/
		public function GameStart()
		{
			//游戏加载未完成暂时不显示，防止点击出错
			this.btn_start.visible=false;
			//监听界面是否关闭
			this.once(Event.CLOSE,this,onClose);
			
			//加载剩余游戏资源、音乐，加载完成与加载进度回调方法
			Laya.loader.load(assetArr,Handler.create(this,onComplete),Handler.create(this,onProgress))
		}
		
		/**
		 * 游戏资源加载完成
		 */
		private function onComplete():void
		{
			//加载完成
			this.txt_load.text="资源加载完成,开始游戏吧...";
			//游戏开始按钮显示并弹出
			this.btn_start.visible=true;
			//缓动类弹出动画
			Tween.from(this.btn_start,{y:this.btn_start.y+20},1000,Ease.elasticOut);
		}
		
		/**
		 * 游戏资源加载进度
		 * @param loadNum  进度
		 */
		private function onProgress(loadNum:Number):void
		{
			//显示加载进度
			this.txt_load.text="资源加载中，当前进度："+parseInt(loadNum*100)+"%";
		}
		
		/**
		 * 界面关闭
		 */
		private function onClose():void
		{
			//从舞台移除自己
			this.removeSelf();
			//只加载一次，因此直接消毁自己
			this.destroy();
		}
	}
}
```



### “Map page” logic function

The logic in the map page is relatively small, and at present we are mainly to roll the background. Establish a GameMap.as class that inherits from GameBgUI.as.

The GameBgUI class name in the main class Main is modified to be GameMap.

The following is the code in GameMap, with only one method, updateMap (), and the map moves up and up, and this method needs to be called in the game master loop.

```
package
{
	import laya.display.Sprite;
	import ui.GameBgUI;	
	/**
	 游戏背景
	 */	
	public class GameMap extends GameBgUI
	{
		public function GameMap()
		{
		}
		/**
		 游戏背景移动更新
		 */		
		public function updateMap():void
		{
			//地图每帧在y向下移动1像素，根据喜好调整
			this.y+=1;
			//如果背景图到了下面不可见，立即调整位置到上方继续循环
			//游戏舞台高为1280
			if (bg1.y + this.y >= 1280) 
			{ 
				bg1.y -= 1280 * 2;
			}
			if (bg2.y + this.y >= 1280) 
			{
				bg2.y -= 1280 * 2;
			}
		}
	}
}
```
In the main class gameInit () method, we add the game frame loop event and callback method loop (), update the map every frame, and write many logic codes later, which will run in this callback method. In the main loop, the map update method is called to observe whether the game map is moved and the speed is reasonable.

```
			......
			//模拟游戏结束，3秒时间
			Laya.timer.once(3000,this,gameOver);
			//增加游戏主循环
			Laya.timer.frameLoop(1,this,loop);
		}
		/**
		 游戏主循环
		 */
		private function loop():void
		{
			//地图滚动更新
			map.updateMap();
		}
```



### “Game page" logic function

Create a page class GamePlay.as in the game, inherit from GamePlayUI.as, and modify the GamePlayUI in the main class Main to be GamePlay.

The main functions of the “game” page are:

1. game pause, display pause page. In the LayaAir engine, the time object is global, so we can make the game time scale to 0, then the game stops. It includes all role animation, background map movement, game main cycle and so on.

2. games continue, pause page disappears, Game Time zoom to 1, game continue.

3. Update the game value changes, the protagonist of blood, game level, score. Create an update () method to update the data.

   In the main class, add the game protagonist blood volume, the barrier, the score global static variable (in other classes can also be modified), and is called in the main class main loop.

```
		/**主角血量***/
		private var hp:int=10;
		/**游戏关卡数***/
		public static var level:int=1;
		/**玩家得分***/
		public static var score:int=0;
		
		
		public function Main()
		{
			//初始化引擎，建议增加WebGl模式
			Laya.init(720, 1280,WebGL);
			......
```

The specific code of the GamePlay class is as follows:

```
package
{
	import laya.events.Event;	
	import ui.GamePlayUI;	
	
	/**
	 * 游戏内UI,血量、积分、等级显示、暂停等
	 * @author CHENZHENG
	 * 
	 */	
	public class GamePlay extends GamePlayUI
	{
		/**
		 * 游戏内UI,血量、积分、等级显示、暂停等
		 */
		public function GamePlay()
		{
			//监听暂停按钮事件
			this.btn_pause.on(Event.MOUSE_DOWN,this,onPause)
			//隐藏暂停提示，也可在IDE中设置为false
			this.gamePause.visible=false;
		}
		
		/**
		 游戏暂停
		 */	
		private function onPause():void
		{
			//显示暂停界面
			this.gamePause.visible=true;
			//暂停界面加点击监听
			this.gamePause.once(Event.MOUSE_DOWN,this,onContinue)
			//时间对象缩放为0就是停止
			Laya.timer.scale=0;
		}
		
		/**
		 游戏继续
		 */	
		private function onContinue():void
		{
			//隐藏暂停界面
			this.gamePause.visible=false;
			//时间对象缩放为1就是正常速度播放
			Laya.timer.scale=1;
		}
		
		/****角色属性UI更新***/
		public function update(hp:int,level:int,score:int):void
		{
			//角色血量更新
			this.txt_hp.text="HP:"+hp;
			//关卡等级更新
			this.txt_level.text="LEVEL:"+level;
			//游戏分数更新
			this.txt_score.text="SCORE:"+score;
		}
	}
}
```



### “End Page" logical function

Set up the end page class GameOver.as, inherit from GameOverUI.as, and modify the GameOverUI in the main class Main to be GameOver.

The main function of "game end" page is:

1.Monitoring “restart“ button click events. In the last lesson, we talked about adding animation effects to page elements, named ani_restart, where we can call programs. The logic is to play button animation after clicking, and then the game is restarted again after the button animation ends.

2. The monitor button dynamic animation completes the event, after the animation completes, it sends the regame start event to the monitor in the main class, and starts the game again.

3. Modify the gameOver () method in the Main class. Modify the mouse monitor as an event monitor.

```
	//重新开始游戏按钮监听,点击后进入游戏中
	over.btn_restart.on(Event.MOUSE_DOWN,this,gameInit);
```
修改为：
```
	//重新开始事件监听,点击后进入游戏中
	over.on("reStart",this,gameInit);
```
GameOver.as类全部代码如下：

```
package
{
	import laya.events.Event;
	import laya.utils.Ease;
	import laya.utils.Handler;
	import laya.utils.Tween;
	
	import ui.GameOverUI;
	/**
	 * 游戏结束界面
	 * @author CHENZHENG
	 */
	public class GameOver extends GameOverUI
	{
		public function GameOver()
		{
			//"重新开始"按钮按下鼠标事件
			this.btn_restart.on(Event.MOUSE_DOWN,this,onRestart);
		}
		/**
		游戏重新开始
		 */		
		private function onRestart():void
		{
			//播放IDE中编辑的按钮动画
			this.ani_restart.play(0,false);
			//监听动画完成事件
			this.ani_restart.once(Event.COMPLETE,this,AniComplete);
		}
		/**
		 按钮动画播放完成
		 */
		private function AniComplete():void
		{
			//发送重新开始事件，在Main类中监听
			this.event("restart")
            //缓动动画关闭效果。IDE中页面为Dialog类型才可用
			start.close();
		}
	}
}
```

Here, our page logic code is complete, compile and run the game to see the final effect.



### Main class all code

```
package {
	
	import laya.display.Stage;
	import laya.events.Event;
	import laya.net.Loader;
	import laya.utils.Handler;
	import laya.webgl.WebGL;
	
	
	public class Main
	{
		/**开始页面***/
		private var start:GameStart
		/**地图页面***/
		private var map:GameMap
		/**游戏中界面***/
		private var play:GamePlay
		/**游戏结束页面***/
		private var over:GameOver
		
		/**主角血量***/
		private var hp:int=10;
		/**游戏关卡数***/
		private var level:int=1;
		/**玩家得分***/
		private var score:int=0;
		
		
		public function Main()
		{
			//初始化引擎，建议增加WebGl模式
			Laya.init(720, 1280,WebGL);
			//全屏不等比缩放模式
			Laya.stage.scaleMode = Stage.SCALE_EXACTFIT;
			//加载游戏页面资源(如果界面资源太多太大[超过50k],建议开始页面单独建立文件夹打包)
			Laya.loader.load("res/atlas/gameUI.atlas",Handler.create(this,this.gameStart))
				
		}
		
		/**
		 资源加载完成后，加载游戏开始界面
		 */
		private function gameStart():void
		{
			//实例化开始页面
			start=new GameStart();
			//以弹出方式打开，有缓动效果。IDE中页面为Dialog才可用
			start.popup();
			//监听开始游戏开始按钮事件,点击后进入游戏中
			start.btn_start.on(Event.MOUSE_UP,this,gameInit)
		}
		
		/**
		 游戏中，游戏初始化
		 */
		private function gameInit():void
		{
			//缓动动画关闭效果。IDE中页面为Dialog才可用
			start.close();
			
			//实例化地图背景页面(如果已实例化，不需要重新new)
			map||=new GameMap();
			//加载到舞台
			Laya.stage.addChild(map);
			
			//实例化游戏中UI页面(如果已实例化，不需要重新new)
			play||=new GamePlay();
			
			//加载到舞台
			Laya.stage.addChild(play);
			
			//模拟游戏结束，3秒时间
			Laya.timer.once(3000,this,gameOver);
			//游戏主循环
			Laya.timer.frameLoop(1,this,loop);
		}
		
		/**
		 游戏主循环
		 */
		private function loop():void
		{
			//地图滚动更新
			map.updateMap()
			//本局游戏数据更新
			play.update(hp,level,score)
		}

		/**
		 游戏结束
		 */
		private function gameOver():void
		{
			//移除地图背景
			map.removeSelf();
			//移除游戏中UI
			play.removeSelf();
			
			//实例化游戏结束页面(如果已实例化，不需要重新new)
			over||=new GameOver();
			//游戏积分显示
			over.txt_score.text=score.toString();
			//以弹出方式打开，有缓动效果。IDE中页面为Dialog才可用
			over.popup();
			//重新开始事件监听,点击后进入游戏中
			over.on("reStart",this,gameInit);
		}
	}
}
```



